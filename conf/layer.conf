# We have a conf and classes directory, add to BBPATH
# Note how we PREpend BBPATH. This is due to the fact that
# bitbake does not override classes per layer as it does
# recipes. Classes are processed strictly in BBPATH order.
# As we unfortunately have to override standard classes for
# bug fixing, we prepend instead of append our path.

#This layer is a stripped down version of the AMP meta-alb layer
#meant to be used independently outside the AMP solution.
BBPATH =. "${LAYERDIR}:"

BBFILE_COLLECTIONS += "desktop-distro"
BBFILE_PATTERN_desktop-distro := "^${LAYERDIR}"
BBFILE_PRIORITY_desktop-distro = "8"

BBFILES += "${@' '.join('${LAYERDIR}/dynamic-layers/%s/recipes*/*/*.bbappend' % layer \
		for layer in BBFILE_COLLECTIONS.split())}"
BBFILES += "${@' '.join('${LAYERDIR}/dynamic-layers/%s/recipes*/*/*.bb' % layer \
		for layer in BBFILE_COLLECTIONS.split())}"

BBFILES += " \
        ${LAYERDIR}/images/*.bb* \
        ${LAYERDIR}/packagegroups/*.bb* \
        ${LAYERDIR}/recipes-*/*/*.bb \
        ${LAYERDIR}/recipes-*/*/*.bbappend \
        ${LAYERDIR}/classes/*.bb* \
"

BBMASK += "recipes-gnome/gnome-desktop/gnome-desktop3_%.bbappend"
BBMASK += "meta-freescale/recipes-graphics/gtk\+/gtk.*\.bbappend"

PREFERRED_PROVIDER_qemu = "qemu"
PREFERRED_VERSION_qemu-native = "4.2.1"
PREFERRED_VERSION_qemu = "4.2.1"

IMX_GPU_VERSION  = "6.4.3.p4.2d"
PREFERRED_VERSION_imx-gpu-viv = "${IMX_GPU_VERSION}-aarch64"

LAYERSERIES_COMPAT_desktop-distro = "hardknott honister kirkstone"

MACHINE_FEATURES:remove = "jailhouse"

FSL_BIN_MIRROR ?= "${FSL_MIRROR}"
